cmake_minimum_required(VERSION 3.20)
option(ENABLE_TESTS "Build tests" ON)

if(ENABLE_TESTS AND VCPKG_TOOLCHAIN)
  list(APPEND VCPKG_MANIFEST_FEATURES "tests")
endif()

project(iptview LANGUAGES CXX)

set(CMAKE_CXX_STANDARD 20)
set(CMAKE_CXX_STANDARD_REQUIRED ON)

if(DEFINED ENV{IPTVIEW_VERSION_MAJOR})
    SET(IPTVIEW_VERSION_MAJOR "$ENV{IPTVIEW_VERSION_MAJOR}")
endif()
if(DEFINED ENV{IPTVIEW_VERSION_MINOR})
    SET(IPTVIEW_VERSION_MINOR "$ENV{IPTVIEW_VERSION_MINOR}")
endif()
if(DEFINED ENV{IPTVIEW_VERSION_PATCH})
    SET(IPTVIEW_VERSION_PATCH "$ENV{IPTVIEW_VERSION_PATCH}")
endif()

if(NOT IPTVIEW_VERSION_MAJOR)
    SET(IPTVIEW_VERSION_MAJOR "0")
endif()
if(NOT IPTVIEW_VERSION_MINOR)
    SET(IPTVIEW_VERSION_MINOR "0")
endif()
if(NOT IPTVIEW_VERSION_PATCH)
    SET(IPTVIEW_VERSION_PATCH "1")
endif()

SET(IPTVIEW_COPYRIGHT "Copyright (c) 2022 Sergiu Giurgiu <sgiurgiu11@gmail.com>")
add_definitions(-DIPTVIEW_VERSION="${IPTVIEW_VERSION_MAJOR}.${IPTVIEW_VERSION_MINOR}.${IPTVIEW_VERSION_PATCH}")
add_definitions(-DIPTVIEW_STRING="M3U Edit v${IPTVIEW_VERSION_MAJOR}.${IPTVIEW_VERSION_MINOR}.${IPTVIEW_VERSION_PATCH}")

if(NOT CMAKE_BUILD_TYPE)
    set(CMAKE_BUILD_TYPE Release CACHE STRING "Build type" FORCE)
endif()

set(EXECUTABLE_NAME iptview)
set(LIBRARY_NAME iptviewlib)

set(CMAKE_AUTOMOC ON)
set(CMAKE_AUTORCC ON)
set(CMAKE_AUTOUIC ON)

find_package(Threads REQUIRED)
find_package(Qt6 COMPONENTS Core Gui Test Widgets OpenGLWidgets Svg Network REQUIRED)

set(IPTVIEW_APP_LIBARIES ${CMAKE_DL_LIBS} Threads::Threads Qt6::Core)
set(IPTVIEW_APP_INCLUDES)

set(IPTVIEW_LIB_LIBARIES Qt6::Core Qt6::Widgets Qt6::OpenGLWidgets Qt6::Svg Qt6::Network)
set(IPTVIEW_LIB_INCLUDES "${CMAKE_CURRENT_SOURCE_DIR}/src")

set(IPTVIEW_TEST_LIBARIES Qt6::Core Qt6::Test ${CMAKE_DL_LIBS} Threads::Threads)
set(IPTVIEW_TEST_INCLUDES)

if(UNIX)
    find_package(PkgConfig REQUIRED)
    pkg_check_modules(MPV REQUIRED IMPORTED_TARGET mpv)
    list(APPEND IPTVIEW_LIB_LIBARIES PkgConfig::MPV)
endif()
if(WIN32)
    find_library(LIBMPV mpv ${LIBMPV_DIR})
    list(APPEND IPTVIEW_LIB_LIBARIES ${LIBMPV} "crypt32.lib")
#    list(APPEND IPTVIEW_LIB_INCLUDES "win" ${LIBMPV_INCLUDE})
endif()

include(GNUInstallDirs)
include(CMakePackageConfigHelpers)

add_subdirectory(src)

if(ENABLE_TESTS)
   add_subdirectory(test)
endif()

SET(CPACK_PACKAGE_NAME "iptview")
SET(CPACK_PACKAGE_DESCRIPTION_SUMMARY "IPTV View Application")
SET(CPACK_RESOURCE_FILE_LICENSE_PROVIDED FALSE)
SET(CPACK_INTERACTIVE FALSE)
SET(CPACK_INCLUDE_SUBDIR TRUE)
SET(CPACK_PACKAGE_VERSION_MAJOR ${IPTVIEW_VERSION_MAJOR})
SET(CPACK_PACKAGE_VERSION_MINOR ${IPTVIEW_VERSION_MINOR})
SET(CPACK_PACKAGE_VERSION_PATCH ${IPTVIEW_VERSION_PATCH})
SET(CPACK_PACKAGE_CONTACT "Sergiu Giurgiu <sgiurgiu11@gmail.com>")
SET(CPACK_PACKAGE_VENDOR "Sergiu Giurgiu <sgiurgiu11@gmail.com>")

set(CPACK_RPM_RELOCATION_PATHS "/${CMAKE_INSTALL_SYSCONFDIR}" "${CMAKE_INSTALL_BINDIR}")
set(CPACK_INCLUDE_TOPLEVEL_DIRECTORY 0) # don't prepend package name
#set(CPACK_PACKAGING_INSTALL_PREFIX ${CMAKE_INSTALL_PREFIX})
#set(CPACK_RPM_POST_INSTALL_SCRIPT_FILE "${CMAKE_CURRENT_SOURCE_DIR}/postinstall")

SET(CPACK_PACKAGE_FILE_NAME "${CPACK_PACKAGE_NAME}-${CPACK_PACKAGE_VERSION_MAJOR}.${CPACK_PACKAGE_VERSION_MINOR}.${CPACK_PACKAGE_VERSION_PATCH}")
if(CPACK_DISTRIBUTION)
    SET(CPACK_PACKAGE_FILE_NAME "${CPACK_PACKAGE_FILE_NAME}-${CPACK_DISTRIBUTION}")
endif()
set(CPACK_PACKAGE_EXECUTABLES "${EXECUTABLE_NAME};M3U Edit")
if(CPACK_GENERATOR STREQUAL "WIX")
    set(CPACK_WIX_UPGRADE_GUID "2a9d812f-6f64-4c1b-b250-dbde0216e869")
    #set(CPACK_WIX_PRODUCT_ICON "${CMAKE_SOURCE_DIR}/icons/reddit_icon_48.png")
    #set(CPACK_PACKAGE_ICON "${CMAKE_SOURCE_DIR}/icons/reddit_icon_48.png")
    set(CPACK_WIX_PROGRAM_MENU_FOLDER "M3U Edit")
    set(CPACK_RESOURCE_FILE_LICENSE "${CMAKE_SOURCE_DIR}/gpl-3.0.rtf")
    set(CPACK_PACKAGE_INSTALL_DIRECTORY "M3U Edit")
endif()


INCLUDE(CPack)
